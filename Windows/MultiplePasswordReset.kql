//Modified and tuned version of multiple password reset alert.
let Threshold=1;
SecurityEvent
| where TimeGenerated >ago(1d)
| where EventID in ("4723","4724")
| extend SubjectUserSid = tostring(SubjectUserSid)
| extend SubjectAccount = strcat(tostring(SubjectDomainName), "\\", tostring(SubjectUserName))
| extend AccountType = case(Account endswith "$" or SubjectUserSid in ("S-1-5-18", "S-1-5-19", "S-1-5-20"), "Machine", isempty(SubjectUserSid), "", "User")
| project TimeGenerated, Computer, AccountType, SubjectAccount, TargetUserName, EventID
| summarize
        StartTimeUtc = min(TimeGenerated),
        EndTimeUtc = max(TimeGenerated),
        ComputerList = make_set(Computer, 25),
        AccountType = make_set(AccountType, 25),
        TargetUserList = make_set(TargetUserName, 25),
        EventIDs=make_set(EventID,3),
        TotalTargets=count() by  SubjectAccount
| where TotalTargets > Threshold
| extend ActivityDescription="SubjectAccount did password reset for multiple users from TargetuserList"
| project-reorder StartTimeUtc,EndTimeUtc, SubjectAccount, ComputerList, TargetUserList, AccountType,EventIDs, TotalTargets, ActivityDescription
